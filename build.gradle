import org.gradle.internal.os.OperatingSystem

plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
    id 'signing'
    id "org.gradle.test-retry" version "1.1.9"

}

def jarVersion = "2.2.3"
group = 'io.nats'

def isRelease = System.getenv("BUILD_EVENT") == "release"
def brn = System.getenv("BRANCH_REF_NAME")
def snap = brn == null || brn.equals("") ? "-SNAPSHOT" : "." + brn + "-SNAPSHOT"

// version is the variable the build actually uses.
version = isRelease ? jarVersion : jarVersion + snap

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

dependencies {
    implementation 'io.nats:jnats:2.24.1'
    implementation("com.fasterxml.jackson.core:jackson-core:2.19.0")
    implementation("io.netty:netty-handler:4.2.7.Final")
    implementation(platform("io.vertx:vertx-stack-depchain:4.5.22"))
    implementation("io.vertx:vertx-core:4.5.22")
    implementation("io.netty:netty-resolver-dns-native-macos:4.2.7.Final")

    testImplementation("io.vertx:vertx-junit5")
    testImplementation 'io.nats:jnats-server-runner:2.0.0'
}

test {
    useJUnitPlatform()
    maxHeapSize = "1g"
    testLogging {
        exceptionFormat = 'full'
        events "started", "passed", "skipped", "failed"
    }
    retry {
        failOnPassedAfterRetry = false
        maxFailures = 20
        maxRetries = 10
    }
    maxParallelForks = 1
}

javadoc {
    options.overview = 'src/main/javadoc/overview.html' // relative to source root
    source = sourceSets.main.allJava
    title = "NATS.IO Java Vert.x Client lib"
    classpath = sourceSets.main.runtimeClasspath
    doLast {
        if (!OperatingSystem.current().isWindows()) {
            exec {
                println "Updating favicon on all html files"
                workingDir 'build/docs/javadoc'
                // Only on linux, mac at this point
                commandLine 'find', '.', '-name', '*.html', '-exec', 'sed', '-i', '-e', 's#<head>#<head><link rel="icon" type="image/ico" href="favicon.ico">#', '{}', ';'
            }
            copy {
                println "Copying images to javadoc folder"
                from 'src/main/javadoc/images'
                into 'build/docs/javadoc'
            }
        }
    }
}


task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
            username = System.getenv('OSSRH_USERNAME')
            password = System.getenv('OSSRH_PASSWORD')
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = rootProject.name
                packaging = 'jar'
                groupId = group
                artifactId = archivesBaseName
                description = 'Java NATS.io Vert.x thin client wrapper.'
                url = 'https://github.com/nats-io/java-nats-server-runner'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = "synadia"
                        name = "Synadia"
                        email = "info@synadia.com"
                        url = "https://nats.io"
                    }
                }
                scm {
                    url = 'https://github.com/nats-io/java-nats-server-runner'
                }
            }
        }
    }
}

if (isRelease) {
    signing {
        def signingKeyId = System.getenv('SIGNING_KEY_ID')
        def signingKey = System.getenv('SIGNING_KEY')
        def signingPassword = System.getenv('SIGNING_PASSWORD')
        useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
        sign configurations.archives
        sign publishing.publications.mavenJava
    }
}

tasks.register('copyToLib', Copy) {
    into "build/libs"
    from configurations.runtimeClasspath
    from configurations.testRuntimeClasspath
}
